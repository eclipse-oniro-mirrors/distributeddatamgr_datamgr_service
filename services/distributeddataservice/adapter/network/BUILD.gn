# Copyright (c) 2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("//foundation/distributeddatamgr/datamgr_service/datamgr_service.gni")

config("network_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "${data_service_path}/adapter/network/src",
    "${data_service_path}/framework/include/network",
  ]
  if (datamgr_service_cloud) {
    include_dirs += [ "${data_service_path}/adapter/include/communicator" ]
  }
}

ohos_source_set("distributeddata_network") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
    boundary_sanitize = true
    ubsan = true
  }
  sources = []
  cflags_cc = [
    "-fvisibility=hidden",
    "-Oz",
  ]

  configs = [ ":network_public_config" ]
  cflags = [
    "-fdata-sections",
    "-ffunction-sections",
    "-Werror",
    "-Wno-multichar",
    "-Wno-c99-designator",
    "-D_LIBCPP_HAS_COND_CLOCKWAIT",
    "-Oz",
  ]

  external_deps = [ "kv_store:datamgr_common" ]

  if (datamgr_service_cloud) {
    sources += [ "src/network_delegate_normal_impl.cpp" ]
    deps = [
      "${data_service_path}/adapter/communicator:distributeddata_communicator",
    ]
    external_deps += [
      "device_manager:devicemanagersdk",
      "hilog:libhilog",
      "netmanager_base:net_conn_manager_if",
    ]
  } else {
    sources += [ "src/network_delegate_default_impl.cpp" ]
  }

  subsystem_name = "distributeddatamgr"
  part_name = "datamgr_service"
}
